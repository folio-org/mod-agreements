package externalEntitlement;
import java.util.concurrent.ExecutionException
import java.util.concurrent.Future

import org.olf.erm.ExternalEntitlement

import groovy.transform.*
import groovyx.net.http.HttpException

@Field ExternalEntitlement externalEntitlement
final String objectProperty = 'reference_object'
def remoteObjValue
if(externalEntitlement.respondsTo(objectProperty)){
  try {
    remoteObjValue = externalEntitlement.invokeMethod(objectProperty, null)
    if (remoteObjValue instanceof Future) {
      remoteObjValue = (remoteObjValue as Future)?.get()
    }
  } catch (ExecutionException e) {
    
    if (e.cause instanceof HttpException) {
      HttpException httpEx = e.cause as HttpException
      remoteObjValue = [error: httpEx.statusCode, message: httpEx.message ]
    } else {
      remoteObjValue = [error: e.class.name, message: e.message]
    }
  }
}

json {
  'id' externalEntitlement.id
  'type' 'external'
  'authority' externalEntitlement.getAuthority()
  'reference' externalEntitlement.getReference()  
  'explanation' externalEntitlement.getExplanation()
  'startDate' externalEntitlement.getActiveFrom()
  'endDate' externalEntitlement.getActiveTo()
  'contentUpdated' externalEntitlement.getContentUpdated()
  'haveAccess' externalEntitlement.getHaveAccess()
  
  if (remoteObjValue) {
    call( objectProperty, remoteObjValue )
  }
}
